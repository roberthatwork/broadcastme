apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

// Don't need these task, so disabling them. Makes it possible to avoid
// declaring a single application main class.
startScripts.enabled = false
run.enabled = false

// Also don't need the regular application distribution packages since
// this is just a set of samples. So disabling to make the build output
// cleaner
distTar.enabled=false
distZip.enabled=false

applicationName = 'broadcastme'
version = ''

jar {
	baseName = 'broadcastme'
    version =  version
    manifest {
        attributes 'Implementation-Title': 'Live Video Streaming using Solace Messaging',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Solace Messaging API for Java Dependencies
    compile("com.solacesystems:sol-jcsmp:10.+")
}

task createAllStartScripts() << {
     // just a placeholder
}
  def scripts = [ 
		 'outputProxy':'com.solace.demos.broadcastme.OutputProxy',
		 'inputProxy':'com.solace.demos.broadcastme.InputProxy'
]
  scripts.each() { scriptName, className ->
          def t = tasks.create(name: scriptName+'StartScript', type: CreateStartScripts) {
        mainClassName = className
        applicationName = scriptName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
      applicationDistribution.into("bin") {
            from(t)
            fileMode = 0755
			duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    createAllStartScripts.dependsOn(t)
}

installDist {
    destinationDir = new File(project.buildDir, 'staged')
}

assemble.dependsOn installDist
